module org.eclipse.xtext.xtext.generator.parser.antlr.splitting.GenerateSimpleExpressions

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = ".."

Workflow {
	
	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = "org.eclipse.xtext.xtext.generator"
				rootPath = rootPath
				runtime = {
					ecoreModel = "${rootPath}/org.eclipse.xtext.xtext.generator/src-gen/org/eclipse/xtext/xtext/generator/parser/antlr/splitting"
				}
				eclipsePlugin = {
					enabled = false
				}
				genericIde = {
					enabled = false
				}
				createEclipseMetaData = false
			}
			code = {
				encoding = "UTF-8"
				lineDelimiter = "\n"
				fileHeader = "/*\n * generated by Xtext\n */"
				preferXtendStubs = false
			}
		}
		language = XtextGeneratorLanguage {
			name = "org.eclipse.xtext.xtext.generator.parser.antlr.splitting.SimpleExpressions"
			fileExtensions = "simpleexpressions"

			fragment = parser.antlr.XtextAntlrGeneratorFragment2  {}
			fragment = ecore.EMFGeneratorFragment2 {}
			fragment = grammarAccess.GrammarAccessFragment2 {}
		}
	}
}
